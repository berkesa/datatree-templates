apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'jacoco'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

// --- JAVADOC ---

javadoc {
	options.encoding = 'UTF-8'
	failOnError = true
	exclude "**/html/**"
}

// --- JACOCO ---

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

// --- CONFIGURATIONS ---

configurations {
    runtime
    ecj
}

configurations.all {
    // resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

sourceSets {
	main {
		java {
			srcDirs= ["src/main/java"]
		}
		resources {
			srcDirs= ["src/test/java"]
		}
	}
}

// --- REPOSITORIES ---

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

// --- DEPENDENCIES ---

dependencies {

    // ================== COMPILATION =========================
    
    testCompile 'junit:junit:4.12'
	ecj 'org.eclipse.jdt.core.compiler:ecj:4.4.2'

	// ================== DATATREE CORE =======================
	    
    // https://mvnrepository.com/artifact/com.github.berkesa/datatree-core
	compile group: 'com.github.berkesa', name: 'datatree-core', version: '1.0.9'
	
	// =========== TEMPLATE ENGINES FOR TESTING ===============
	
	// --- FREEMARKER ---
	
	// https://mvnrepository.com/artifact/org.freemarker/freemarker
	compile group: 'org.freemarker', name: 'freemarker', version: '2.3.28'
	
	// --- JADE ---
	
	// https://mvnrepository.com/artifact/de.neuland-bfi/jade4j
	compile group: 'de.neuland-bfi', name: 'jade4j', version: '1.2.7'

	// --- MUSTACHE ---
		
	// https://mvnrepository.com/artifact/com.github.spullara.mustache.java/compiler
	compile group: 'com.github.spullara.mustache.java', name: 'compiler', version: '0.9.6'

	// --- THYMELEAF ---
	
	// https://mvnrepository.com/artifact/org.thymeleaf/thymeleaf
	compile group: 'org.thymeleaf', name: 'thymeleaf', version: '3.0.11.RELEASE'

	// --- PEBBLE ---
	
	// https://mvnrepository.com/artifact/com.mitchellbosecke/pebble
	compile group: 'com.mitchellbosecke', name: 'pebble', version: '2.4.0'
	
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.github.berkesa'

version = '1.1.1'
  
modifyPom {
    project {
        artifactId 'datatree-templates'
        name 'DataTree Templates'
        description 'Template Engine based on DataTree API.'
        url 'https://berkesa.github.io/datatree-templates/'
        inceptionYear '2019'

        scm {
            url 'https://berkesa.github.io/datatree-templates/'
            connection 'scm:https://github.com/berkesa/datatree-templates.git'
            developerConnection 'scm:git://github.com/berkesa/datatree-templates.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        
        dependencies {
            dependency {
                groupId 'com.github.berkesa'
                artifactId 'datatree-core'
                version '1.0.9'
                scope 'runtime'
            }        
        }

        developers {
            developer {
                id 'berkesa'
                name 'Andras Berkes'
                email 'andras.berkes@programmer.net'
            }
        }
    }
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
	snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}

// --- COMPILATION ---

compileJava {
	options.fork = true
	options.forkOptions.with {
		executable = 'java'
		jvmArgs = ['-classpath', project.configurations.ecj.asPath, 'org.eclipse.jdt.internal.compiler.batch.Main', '-nowarn']
	}
}